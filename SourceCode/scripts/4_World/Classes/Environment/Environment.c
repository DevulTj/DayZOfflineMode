class Environment
{
	
	// Average day and night temperatures - 0J 1F 2M 3A 4M 5J 6J 7A 8S 9O 10N 11D
	protected float	   				dayTemperatures[12]={ 1, 3, 5,9,11,20,22,15,11, 5,  4,  0};
	protected float  				nightTemperatures[12]={-5,-4, -1, 6,9,15,15,10, 6, 4, -1, -5};
	
	protected float					m_TicksCount = 0; //ticks passed since last clothing wetting or drying
	protected float 				m_WetDelay = ENVIRO_TICKS_TO_WETNESS_CALCULATION; //koef compensating for delayed wetting processing

	//player
	protected PlayerBase 			m_Player;
	protected float					m_PlayerHeightPos; // y position of player above water level (meters)
	protected float 				m_PlayerSpeed; // 1-3 speed of player movement
	protected float 				m_PlayerTemperature; //34-44
	protected float 				m_PlayerHeat; //3-9 heatcomfort generated by entites movement
	
	protected float					m_HeatComfort;  //delta that changes entitys temperature				
	protected float					m_ItemsHeat; // temperature of items in inventory modified by heat transfer koeficient
	protected float					m_ItemsHeatIsolation; // 0-30  isolation of clothes, fresh spawn heatisolation = 5.5 + 2.5 + 4a cca 12, cca 20 in sweater		
	
	//environment
	protected float 				m_Rain = 0; // 0-1 amount of rain 
	protected float 				m_Wind = 0; // strength of wind
	protected float 				m_Fog = 0; // 0-1 how foggy it is
	protected float 				m_DayOrNight = 0; // 0-1 day(1) or night(0)
	protected float 				m_Clouds = 0; // 0-1 how cloudy it is
	protected float 				m_EnvironmentTemperature = 0; //temperature of environment player is in
	protected int					m_CurrentMonth = 0; // 0-11 used fo proper air temperature selection
	protected float					m_Time = 0;

	void Environment(PlayerBase player)
	{
		Init(player);
	}
	
	void Init(PlayerBase player)
	{
		m_Player 			= player;
		m_PlayerSpeed		= 0.0;
	}

	// Calculates heatisolation of clothing, process its wetness, collects heat from heated items and calculates player's heat comfort
	void Update(float delta_t)
	{		
		if (m_Player)
		{
			m_Time += delta_t;
			if ( m_Time >= ENVIRO_TICK_RATE )
			{
				m_Time = 0;
				m_TicksCount++; // Sets whether it is time to add wetness to items and clothing
						
				//! Updates data
				CollectAndSetPlayerData();
				CollectAndSetEnvironmentData();
				
				//! Process wetness and temperature
				ProcessItemsHeat();
				if ( m_TicksCount >= ENVIRO_TICKS_TO_WETNESS_CALCULATION )
				{
					//! adds wetness to items
					ProcessItemsWetness();
					m_TicksCount = 0;
				}
			}
		}
	}
			
	//! Returns heat player generated based on player's movement speed (for now)
	float GetPlayerHeat()
	{
		//float heat = Math.Max(m_PlayerSpeed*ENVIRO_DEFAULT_ENTITY_HEAT, ENVIRO_DEFAULT_ENTITY_HEAT);
		float heat = Math.Max(m_PlayerSpeed*0.5, 0.5);
		return heat;
	}
	
	//! Checks whether Player is sheltered
	bool IsUnderRoof()
	{
		int hitComponentIndex;
		float hitFraction;
		vector hitPosition, hitNormal 
		vector from = m_Player.GetPosition();
		vector to = from + "0 25 0";
		Object hitObject;
		PhxInteractionLayers collisionLayerMask = PhxInteractionLayers.BUILDING|PhxInteractionLayers.VEHICLE;
		
		return DayZPhysics.RayCastBullet(from, to, collisionLayerMask, hitObject, hitPosition, hitNormal, hitFraction);
	}

	// Calculates item heat isolation based on its wetness
	float GetCurrentItemHeatIsolation( ItemBase item )
	{
		float heat_isolation = g_Game.ConfigGetInt("cfgVehicles " + item.GetType() + " heatIsolation");
		float wet_factor = item.GetWet();
		if ( wet_factor > 0 )
		{
			if ( wet_factor <= ENVIRO_WET_PENALTY )
			{
				heat_isolation = heat_isolation * (1 - wet_factor);
			}
			else
			{
				heat_isolation = -ENVIRO_WET_PENALTY_EFFECT * (wet_factor - ENVIRO_WET_PENALTY);
			}
		}
		return heat_isolation;
	}
	
	//ENVIRONTMENT
	// Returns amount of ?C air temperature should be lowered by, based on player's height above water level
	float GetTemperatureHeightCorrection()
	{
		float temperature_reduction = Math.Max(0, (m_PlayerHeightPos * ENVIRO_TEMPERATURE_HEIGHT_REDUCTION));
		return temperature_reduction;
	}
	
	// Calculates and return temperature of environment
	float GetEnvironmentTemperature()
	{
		float temperature;
		float night_temperature = nightTemperatures[m_CurrentMonth];
		float day_temperature = dayTemperatures[m_CurrentMonth];
		if ( night_temperature < day_temperature )
		{
			temperature = night_temperature + ((day_temperature-night_temperature)*m_DayOrNight);
		}
		else
		{
			temperature = day_temperature + ((night_temperature-day_temperature)*m_DayOrNight);
		}
		if ( m_Player.IsWaterContact() ) 
		{
			temperature = temperature*ENVIRO_WATER_TEMPERATURE_KOEF;
		}
		float fog_effect  = m_Fog*ENVIRO_FOG_TEMP_EFFECT;
		float clouds_effect = m_Clouds*ENVIRO_CLOUDS_TEMP_EFFECT;
		float combined_effect = clouds_effect+fog_effect;
		if ( combined_effect > 0 )
		{
			temperature = temperature*(1-combined_effect);
		}
		temperature -= GetTemperatureHeightCorrection();
		return temperature;
	}	
		
	// Calculats wet/drying delta based on player's location and weather
	float GetWetDelta() 
	{
		float wet_delta = 0;
		if ( m_Player.IsWaterContact() )
		{
			//player is getting wet from swimming in water
			wet_delta = 1;
		}
		else
		{
			if ( m_Rain > 0 && !IsUnderRoof() )
			{
				//player is getting wet from rain
				wet_delta = ENVIRO_WET_INCREMENT * m_WetDelay * (1+(ENVIRO_WIND_EFFECT*m_Wind)) * ( m_Rain );
			}
			else
			{
				//player is drying
				float sun_effect = (ENVIRO_SUN_INCREMENT * m_DayOrNight * (1-m_Fog))*(1-(m_Clouds*ENVIRO_CLOUD_DRY_EFFECT));
				wet_delta = -((ENVIRO_DRY_INCREMENT * m_WetDelay * Math.Sqrt(m_PlayerHeat)) + sun_effect) * (1+(ENVIRO_WIND_EFFECT*m_Wind));
			}
		}		
		return wet_delta;
	}
	

	// EXPOSURE
	// Each tick updates current entity member variables
	void CollectAndSetPlayerData()
	{
		vector playerPos = m_Player.GetPosition();
		m_PlayerHeightPos = playerPos[1];

		HumanCommandMove hcm = m_Player.GetCommand_Move();
		if(hcm)
			m_PlayerSpeed = hcm.GetCurrentMovementSpeed();
		m_PlayerTemperature = m_Player.GetStatTemperature().Get(); //can be current entity temeprature in future
		m_PlayerHeat = GetPlayerHeat();
	}
	
	// Each tick updates current environment member variables
	void CollectAndSetEnvironmentData()
	{
		Weather weather = g_Game.GetWeather();

		m_Rain = weather.GetRain().GetActual();
		m_DayOrNight = (-1 * Math.AbsFloat( ( 1 - ( g_Game.GetDayTime() / ENVIRO_HIGH_NOON ) ) ) ) + 1;
		m_Fog = weather.GetFog().GetActual();
		m_Clouds =	weather.GetOvercast().GetActual();
		m_CurrentMonth = 8; //can be set by server date in future
		m_Wind = weather.GetWindSpeed();
		m_EnvironmentTemperature = GetEnvironmentTemperature();
	}

	// Wets or dry items once in given time 
	void ProcessItemsWetness()
	{
		EntityAI attachment;	
		ItemBase item;
		int attachemnt_count = m_Player.GetInventory().AttachmentCount();
		for (int att = 0; att < attachemnt_count; att++)
		{	
			attachment = m_Player.GetInventory().GetAttachmentFromIndex(att);
			if ( attachment.IsItemBase() )
			{
				if( Class.CastTo(item, attachment) )
				{
					// Calculates temperature and process wetness for all items inside cargo
					if ( !item.KindOf("waterproof") )
					{
						if ( !item.KindOf("rainproof") ) 
						{
							item.AddWet(GetWetDelta());
						}

						if ( m_Player.GetInventory().GetAttachmentFromIndex(att).GetInventory().GetCargo() )
						{
							int icount = m_Player.GetInventory().GetAttachmentFromIndex(att).GetInventory().GetCargo().GetItemCount();

							for (int i = 0; i < icount; i++)
							{
								ItemBase in_item;
								if ( Class.CastTo(in_item, attachment.GetInventory().GetCargo().GetItem(i)) && in_item.IsItemBase() )
								{
									in_item.AddWet(GetWetDelta()*ENVIRO_WET_PASSTHROUGH_KOEF);
								}
							}
						}	
					}
				}
			}
		}
	}

	// Calculates and process temperature of items
	void ProcessItemsHeat()
	{
		EntityAI attachment;	
		ItemBase item;

		m_HeatComfort = 0.0;
		m_ItemsHeat = 0.0;
		m_ItemsHeatIsolation = 0.0;
		
		float heat_comfort;  //delta that changes entitys temperature				
		float items_heat; // temperature of items in inventory modified by heat transfer koeficient
		float items_heat_isolation; // 0-30  isolation of clothes, fresh spawn heatisolation = 5.5 + 2.5 + 4a cca 12, cca 20 in sweater		
		int attachemnt_count = m_Player.GetInventory().AttachmentCount();

		for (int att = 0; att < attachemnt_count; att++)
		{
			attachment = m_Player.GetInventory().GetAttachmentFromIndex(att);

			if ( attachment.IsItemBase() )
			{
				if( Class.CastTo(item, attachment) )
				{
					if ( item.IsClothing() ) 
					{
						m_ItemsHeatIsolation += GetCurrentItemHeatIsolation(item);
					}

					if ( m_Player.GetInventory().GetAttachmentFromIndex(att).GetInventory().GetCargo() )
					{
						int icount = m_Player.GetInventory().GetAttachmentFromIndex(att).GetInventory().GetCargo().GetItemCount();

						for (int i = 0; i < icount; i++)
						{						
							ItemBase in_item;
							if ( Class.CastTo(in_item, attachment.GetInventory().GetCargo().GetItem(i)) && in_item.IsItemBase() )
							{
								m_ItemsHeat += in_item.GetTemperature();
							}
						}
					}
				}
			}
		}
		// Calculate heat gained from items based on its temeprature
		m_ItemsHeat = m_ItemsHeat * ENVIRO_ITEM_HEAT_TRANSFER_KOEF;
		// Sets heatcomfort. Should be 0. More it is below 0 the colder player feels, more is it over 0 the hotter player feels.
		m_HeatComfort = ( m_PlayerHeat + m_ItemsHeat + m_ItemsHeatIsolation - ( m_PlayerTemperature - m_EnvironmentTemperature ) );	
		m_Player.GetStatHeatComfort().Set(m_HeatComfort);
	}

#ifdef DEVELOPER
	void ShowEnvDebugPlayerInfo(bool enabled)
	{
		int windowPosX = 10;
		int windowPosY = 200;

		Object obj;

		DbgUI.Begin("Player stats", windowPosX, windowPosY);
		if( enabled )
		{
			DbgUI.Text("Temperature: " + m_PlayerTemperature.ToString());
			DbgUI.Text("Heat comfort: " + m_HeatComfort.ToString());
			DbgUI.Text("Heat Isolation: " + m_ItemsHeatIsolation.ToString());
		}
		DbgUI.End();
		
		DbgUI.Begin("Weather stats:", windowPosX, windowPosY + 100);
		if( enabled )
		{
			DbgUI.Text("Env temperature: " +  m_EnvironmentTemperature.ToString());
			DbgUI.Text("Wind: " +  m_Wind.ToString());
			DbgUI.Text("Rain: " + m_Rain.ToString());
			DbgUI.Text("Day/Night (1/0): " + m_DayOrNight.ToString());
			DbgUI.Text("Fog: " + m_Fog.ToString());
			DbgUI.Text("Clouds: " + m_Clouds.ToString());
			DbgUI.Text("Height: " + GetTemperatureHeightCorrection().ToString());
			DbgUI.Text("Wet delta: " + GetWetDelta().ToString());
		}
		DbgUI.End();
	}
#endif
};